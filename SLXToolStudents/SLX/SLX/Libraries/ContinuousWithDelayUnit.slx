//*****************************************************************************
// 	Module Continuous
//                           Runge-Kutta-Merson method with step length control
//		                       foward Euler and Euler-Heun with fixed step length
//                           virtual function derivatives without time dependency
//                           Observer with pseudo-graphical curve display
//*****************************************************************************

#define SLX2 ON
#define TRACE
import <STATS>

public module Continuous
{
	statement continuous @wait @until #condition [integrator = #integrator];
	definition
	{
		expand (#condition, (#integrator == "") ? "theIntegrator" : #integrator) |"
			ACTIVE->stateEventFinalized = FALSE;
			while (!ACTIVE->stateEventFinalized) {
				ACTIVE->stateEventDetected = (#);
				wait list = #->waitUntilList;
			}
		"|;	}		OEM static string(*) value_trace_format =
	"____________.________|  ________________|  __._________|  __._________|  _________________________________|  ______.______|  ______._______|  __.______________|\n";
	
	// Global Declarations
	//------------------------------------------------------------------------------------------------
	constant double EPSILON1= 1E-150;
	constant double EPSILON2= 1E-150;
	
	pointer (Integrator) theIntegrator;
	procedure errorMSG (string(*) t1, string(*) t2, string(*) t3, string(*) t4, string(*) t5)
	{
		print options=red (t1, t2, t3, t4, t5) "Continous::errorMSG: _  _  _ _ _\n";
		exit(0);
	}
	
	//*****************************************************************************
	// class Puck ---Extension
	//*****************************************************************************
	augment puck
	{
		boolean stateEventDetected, stateEventFinalized;
	}
	
	//*****************************************************************************
	// class Point
	//*****************************************************************************
	passive class Point (double x_, y_) {
		double x= x_;
		double y= y_;
		procedure show () {
			print options= italic (x, y) "[ __.__, __.__ ]\n";
		}
} //class Point

	//*****************************************************************************
	// class Continous Variable
	//*****************************************************************************
	passive class ContinuousVariable {
		string(22) myName;
		boolean isActive;
		pointer(Integrator) myIntegrator;
		
		control double currentState;
		double state;
		double rate;
		double initialState;
		double errorValue;
		double k1;
		double k2;
		double k3;
		
		method initialize(double v)
		{
			currentState = v;
			state = v;
		}
		
		initial
		{
			get_slx_name(ME, myName);
		}
	} // ContinuousVariable
	
	//*****************************************************************************
	// class Delay Variable
	//*****************************************************************************
	passive class DelayVariable ( int capacity_, double tau_ , accuracy_, initialState_) {
		string(22) 			myName;
		int				capacity= capacity_;
		double 			tau= tau_; //cycles
		double 			accuracy= accuracy_;	
		boolean 			isActive;
		pointer(Integrator)	myIntegrator;
			
		set (Point) 	ranked (ascending x) history ;
		set (Point) 	ranked (ascending x) recycling ;
		random_variable positiveDistance;
		random_variable negativeDistance;
			
		boolean 			historyTrace;			
		double 			currentState;
		double 			historyState;
		double 			currentTime,
						historyTime;
		double 			initialState= initialState_;
						
		pointer (Point) 	recyclePoint;
		boolean printed;
		
		int 
			count_increase_cap,
			count_eachPut,
			count_realPut,	
			count_get,
			count_get_interpolation,
			count_get_left,
			count_get_rounded_left,
			count_get_rounded_right,
			count_get_exact_right,
			count_get_exact_left,
			count_get_identical,
			count_cap_Incr,
			count_move2recycling,
			count_move2history,	
			count_update,
			count_real_update,
			maxSizeHistory;
			
		initial {
			int i;
			get_slx_name(ME, myName);

			for (i=1; i<= capacity; i++) {
				place new Point (time-tau, 0.0) into recycling;
				count_move2recycling++;
			}
			if (history.size > maxSizeHistory) maxSizeHistory= history.size;
			currentState= initialState;
			historyState= initialState;
			historyTime= time - tau;
			currentTime= time;
			place new Point (time-tau, initialState) into history;
	
			//showMemory(myName);
		} // initial
			

		method put ( double v ) {
			pointer (Point) recyclePoint;
			count_eachPut++;
			
			if ( abs( v - currentState ) >= accuracy ) { 
				
				currentState= v;
				currentTime= time;
				count_realPut++;
						
				recyclePoint= first Point in recycling;
				
				if (recyclePoint == NULL) {
					recyclePoint= new Point (time, v);
					capacity++;	
					count_increase_cap++;
				}
				else {
					remove recyclePoint from recycling;
				}
				recyclePoint->x= time;
				recyclePoint->y= v;
				place recyclePoint into history;
				//showMemory("after real put");
				if (history.size > maxSizeHistory) 
					maxSizeHistory= history.size;	
				//else nothing
				count_move2history++;
			}
			//else nothing
		} // method put

			
		method update () {
			pointer (Point) firstP, 
							secondP;
			double 			t_star;			
		
			count_update++;
			t_star= time - tau;
			firstP = first Point in history;
			if ( !(firstP == NULL)) 
			{
				secondP= successor (firstP) in history;
				if (!secondP == NULL) 
				{
					if (secondP ->x  < t_star)  
					{
						count_real_update++;
						remove firstP from history;
						place firstP into recycling;
						//showMemory("after update");
					}	
				}
				
			}
			else {
				print options= red "FATAL ERROR IN update(), empty history \n";
				showMemory("EXIT");
				exit(-1);
			}	
			//historyState= firstP->y;
			//	historyTime= firstP->x;	

		} // method update
		
		
		method findGreatestPointBeforeOrEqualTo (double t) returning pointer (Point){
			pointer (Point) p;
			pointer (Point) saveP;
			
			p= first Point in history;
			if (p==NULL) {
				print options= red "FATAL ERROR IN findGreatestPointBefore(), empty history \n";
				showMemory("EXIT");
				exit(-1);
			}
			
			if (p->x == t) 
				return p;
			
			while (p->x < t ) {
				saveP= p;
				p= successor (p) in history;
				if (p== NULL) return saveP;
			}
			return saveP;
		} // findGreatestPointBefore

				
		method get (double t) returning double {
			pointer (Point) pLeft, pRight;
				
			count_get++;
		
			pLeft = findGreatestPointBeforeOrEqualTo(t);  //pLeft =/= NULL
			
			if (pLeft->x == t ) {
				count_get_exact_left++;
				historyTime= t;
				historyState= pLeft->y;
				return historyState;
			}	
			
			pRight = successor (pLeft) in history;
			
			if ((pRight == NULL) && (recycling.size >0)) {
					pRight= last Point in recycling;
					if (pRight->x >= t) {
						print options= red "FATAL ERROR IN get(), candidate is in recycling \n";
						showMemory("EXIT");
						remove pRight from recycling;
						place pRight into history;
					}
			}
			
			if (pRight == NULL) {
				count_get_left++;
				historyTime= t;
				historyState= pLeft->y;
				return historyState;
			}	
	
			if (pRight->x == t) {
				count_get_exact_right++;
				historyTime= t;
				historyState= pRight->y;
				return historyState;
			}
			
			if (pLeft->x  == pRight->x)  {
				count_get_identical++;
				historyTime= t;
				historyState= pLeft->y;
				return historyState;
			}
			
						
   			if (pRight->x - pLeft->x > EPSILON1) {
				count_get_interpolation++;
				historyTime= t;
				historyState=  (pRight->y - pLeft->y) / (pRight->x - pLeft->x) * (t - pLeft->x) + pLeft->y;
				//tabulate positiveDistance= pRight->x - pLeft->x;

				return historyState;
			}
			else 
			if (pLeft->x - pRight->x >  EPSILON2){
				count_get_interpolation++;
				historyTime= t;
				historyState=  pLeft->y;
				//tabulate negativeDistance= pRight->x - pLeft->x;

				return historyState;
			}
			else {	
				count_get_rounded_right++;
				historyTime= t;
				historyState= pRight->y;
				return historyState;
			}	

		} // method get
		

		method showMemory( string(*) str) {
			pointer (Point) p;
			int i;
			print (time, str, time - tau) "_____.___  :: DELAY ________ delay time= _____.___ s::";
			print (history.size) ":: history (size = ____)  =====> ";
			for (p = each Point in history) {
				i++;
				print ( p->x, p->y) " ( __.__ , ___.__) ..";
				if (i > 2) break;
			}
			p= last Point in history;			
			print ( p->x, p->y) "................(last) = ( __.__ , ___.__)";
				p= last Point in recycling;	
			print (recycling.size) "<======== recycling (size = ____):: ";
			if (!p==NULL) print ( p->x, p->y) "................(last) = ( __.__ , ___.__)";
			print "\n";
		} // method show

		method summary (string(*) str) {
			pointer (Point) p;
			
			print (time, str, tau ) " _____.___ |_____________ with tau= ____.____  time units\n";
			print "**********************************************************\n\n";
			
			print (count_eachPut) "count-each-put= _\n";
			print (count_realPut) "count-real-put= _\n";
			
			print (count_get) "count-get= _\n";
			print (count_get_identical) "count-get-identical= _\n";
			print (count_get_interpolation) "count-get-interpol= _\n";
			print (count_get_left) "count-get-left-border= _\n";
			print (count_get_rounded_left) "count-get-rounde-left-border= _\n";
			print (count_get_rounded_right) "count-get-rounde-right-border= _\n";


			print (count_get_exact_left) "count-get-excact-left-border= _\n";
			print (count_get_exact_right) "count-get-exact-right-border= _\n";
			print (count_increase_cap) "count-increase-cap= _\n";	
			print (count_move2recycling) "count-move-to-recycling= _\n";
			print (count_move2history) "count-move-to-history= _\n";
			print (count_update) "count-each-update= _\n";
			print (count_real_update) "count-real-updatess= _\n";
			print (maxSizeHistory) "max-hisory-size= _\n";
			print "\n";
			
			//report positiveDistance;
			//report negativeDistance;

					
		} // method show
	} // DelayVariable

	//*****************************************************************************
	// class Behavior (as Ordinary Differential Equation)
	//*****************************************************************************
	public interface Behavior {
		
				abstract method derivatives(double t);
	}
	
	type ErrorType enum {RELATIVE, ABSOLUTE};
	
	passive class IntegratorRecord(pointer(Behavior) ode_, int prio_)
	{
		int prio = prio_;
		double t; // internal time
		pointer(Behavior) ode = ode_; // differential equation
	}
	
	//*****************************************************************************
	//class  Integrator
	//*****************************************************************************
	abstract class Integrator(double minStepLength_, double maxStepLength_,
	                          ErrorType errorType_, double errorLimit_)
	{
		double minStepLength = minStepLength_;
		double maxStepLength = maxStepLength_;
		ErrorType errorType = errorType_;
		double errorLimit = errorLimit_;
		
		string(15) myName;
		pointer(puck) myPuck;
		double stepLength;
		double errorValue;		// represents the Euclid or maximum norm
		double savedStepLength;	// in the case that the stepLenght has to be reduced for synchronisation
		double largeStepLength;	// largest stepLength without any synchronisation problem
		
		boolean stateEventHandling;		// TRUE for RKM, FALSE for Euler-Heun
		boolean traceStateStarted;
		boolean traceEventStarted;
		
		set (ContinuousVariable) vList;	// all active ContinuousVariables
		set (DelayVariable) dList;	// all active DelayVariables

		set (IntegratorRecord) ranked (descending prio) rList; // all active DifferentialEquations
		set (puck) pList; // all active time-discrete PeerPucks  for synchronisation
		pointer(puck) waitUntilList;
		// all time-discrete pucks which have detected a state event (by wait until);
		// they will be reactivated as soon the last integration step was repeated
		// with a reduced step length
		
		method attachVar(pointer(ContinuousVariable) v)
		{
			v->myIntegrator = ME;
			v->isActive = TRUE;
			place v into vList;
		}
		
		method detachVar(pointer(ContinuousVariable) v)
		{
			v->isActive = FALSE;
			
			if (vList contains v)
			{
				remove v from vList;
				v->myIntegrator = NULL;
			}
			else
				errorMSG("stop of", v->myName, "for Integrator", myName, "is invalid");
		}
		
		method attachDelayVar(pointer(DelayVariable) d)
		{
			d->myIntegrator = ME;
			d->isActive = TRUE;
			place d into dList;
		}
		
		method detachDelayVar(pointer(DelayVariable) d)
		{
			d->isActive = FALSE;
			
			if (dList contains d)
			{
				remove d from dList;
				d->myIntegrator = NULL;
			}
			else
				errorMSG("stop of", d->myName, "for Integrator", myName, "is invalid");
		}
		

		
		method attachPuck(pointer(puck) p)
		{
			place p into pList;
		}
		
		method detachPuck(pointer(puck) p)
		{
			remove p from pList;
		}
		
		method attachODE(pointer(Behavior) ode, int prio)
		{
			place new IntegratorRecord(ode, prio) into rList;
		}
		
	
		method detachODE(pointer(Behavior) ode)
		{
			pointer(IntegratorRecord) rPtr;
			
			for (rPtr = each IntegratorRecord in rList with rPtr->ode == ode)
			{
				remove rPtr from rList;
				break;
			}
		}
		
				
		method stateEventDetected() returning boolean
		{
			pointer(puck) p;
			
			for (p = waitUntilList; p != NULL; p = p->frozen_successor)
				if (p->stateEventDetected)
					return TRUE;
			
			return FALSE;
		} // stateEventDetected
		
		method stateEventFinalization()
		{
			pointer(puck) p;
			
			for (p = waitUntilList; p != NULL; p = p->frozen_successor)
			{
				if (p->stateEventDetected)
					p->stateEventFinalized = TRUE;
			}
		} // stateEventFinalization
		
		method trace_state_on()
		{
			traceStateStarted = TRUE;
		}
		
		method trace_state_off()
		{
			traceStateStarted = FALSE;
		}
		
		method valueTrace()
		{
			pointer(ContinuousVariable) vPtr;
			double t = time;
			
			print options=bold
			              ("Time", "Integrator", "Step-Length", "Large_h", "Variable", "State",
			               "Rate", "Error-Value") value_trace_format;
			
			for (vPtr = each ContinuousVariable in vList)   // compute the current rate at t
			{
				print (	t, myName, stepLength, largeStepLength, vPtr->myName,
				        vPtr->state, vPtr->rate, errorValue ) value_trace_format;
			}
		}
		
		method trace_event_on()
		{
			traceEventStarted= TRUE;
		}
		
		method trace_event_off()
		{
			traceEventStarted= FALSE;
		}

		abstract method takeAStep(double stepLength);

		overridable method errorNorm() returning double
		{
			//..................................................................
			// evaluates the maximum Norm
			double keepValue = 0.0;
			pointer(ContinuousVariable) vPtr;
			
			for (vPtr = each ContinuousVariable in vList)
			{
				if (vPtr->errorValue > keepValue)
					keepValue = vPtr->errorValue;
			}
			
			return keepValue;
		} // errorNorm
		
		abstract method reduce() returning boolean;
		
		abstract method enlarge();
		
		initial
		{
			get_slx_name (ME, myName);
			
			if (minStepLength <= 0 || maxStepLength <= 0 || minStepLength >= maxStepLength)
			{
				if (minStepLength < 0)
					minStepLength = -minStepLength;
				if (maxStepLength < 0)
					maxStepLength = -maxStepLength;
				if (minStepLength == 0)
					minStepLength = 0.01;
				if (maxStepLength == 0)
					maxStepLength = 0.1;
				if (minStepLength >= maxStepLength)
					maxStepLength *= 10.0;
			}
			
			myPuck = activate ME;
			myPuck->priority = -200;
		}
		
		actions
		{
			pointer(IntegratorRecord) rPtr; 
			pointer(ContinuousVariable) vPtr;
			pointer(DelayVariable) dPtr;
			pointer(puck) wPtr;
			pointer(puck) pPtr;
			
			forever
			{
				// wait for something to do
				wait until rList.size != 0 && vList.size != 0;
				
				// estimate the time distance to the next peer process
				largeStepLength = stepLength;
				for (pPtr = each puck in pList with pPtr->state == SCHEDULED)
				{
					if ((pPtr->move_time - time) <= largeStepLength)
						largeStepLength = pPtr->move_time - time;
				}
				
				// adaptation of the next integration step
				if (stepLength > largeStepLength)
				{
					savedStepLength = stepLength;
					stepLength = largeStepLength;
				}
				
				// save the current state for all active variables at: (t)
				for (vPtr = each ContinuousVariable in vList)
					vPtr->initialState = vPtr->state;			
								
				takeAStep(stepLength);
				
				while (reduce())   // because too large integration error
				{
					stepLength *= 0.5;      // reducing step size for reply
					
					for (vPtr = each ContinuousVariable in vList)
						vPtr->state = vPtr->initialState;
					
					if (traceStateStarted)
						print "**** STEP LENGTH REDUCTION ****\n";
					
					takeAStep(stepLength);
				};
				
				// update of real_state for pending wait untils
				for (vPtr = each ContinuousVariable in vList)
					vPtr->currentState = vPtr->state;
				
				// control has jump to all slaves for computing the current state conditions
				reactivate list = waitUntilList;
				yield;
				
				if (stateEventHandling && stateEventDetected())
				{
					// handle stateEvent
					savedStepLength = stepLength;
					if (traceEventStarted)
						print options= bold, italic
							"--- State Event Handling starts\n";
					
					// perform binary search to find the earliest state event
					while (stepLength >= 2*minStepLength)
					{
						while (stateEventDetected() && stepLength > 2.0*minStepLength)
						{
							if (traceEventStarted)
								print "stateEvent && stepLength > 2.0*minStepLength: the last step was \Bignored\B, next try with stepLength/2\n";
							
							for (vPtr = each ContinuousVariable in vList)
								vPtr->state = vPtr->initialState;
							
							stepLength *= 0.5;
							takeAStep(stepLength);
							
							for (vPtr = each ContinuousVariable in vList)
								vPtr->currentState = vPtr->state;
							
							reactivate list = waitUntilList;
							yield;
						}
						
						if (!stateEventDetected() && stepLength > 2.0*minStepLength)
						{
							forever
							{
								advance stepLength;
								
								///////////////////////////////////////////////////////////////////////
								for (dPtr = each DelayVariable in dList)
								dPtr->update();
								///////////////////////////////////////////////////////////////////////		
								
								for (rPtr = each IntegratorRecord in rList)
									rPtr->t += stepLength;

								
								if (stateEventDetected() || stepLength <= 2.0*minStepLength)
									break;
								
								if (traceEventStarted)
								{
									print "!stateEvent && stepLength > 2.0*minStepLength: the last step was \Baccepted\B, next try with stepLength/2\n";
									valueTrace();
								}
								
								for (vPtr = each ContinuousVariable in vList)
									vPtr->initialState = vPtr->state;
								
								stepLength *= 0.5;
								takeAStep(stepLength);
								
								for (vPtr = each ContinuousVariable in vList)
									vPtr->currentState = vPtr->state;
								
								reactivate list = waitUntilList;
								yield;
							}
						}
					}
					
					reactivate list = waitUntilList;
					yield;
					
					// minStepLength is already in use
					// but the case could happen, that we are shortly before the event
					if (!stateEventDetected())
					{
						stepLength= 2*stepLength;
						takeAStep(stepLength);  // minStepLength
						
						for (vPtr = each ContinuousVariable in vList)
							vPtr->currentState= vPtr->state;
						
						advance stepLength;
						
						///////////////////////////////////////////////////////////////////////
						for (dPtr = each DelayVariable in dList)
								dPtr->update();
						///////////////////////////////////////////////////////////////////////		
						
						for (rPtr = each IntegratorRecord in rList)
							rPtr->t += stepLength;
							
						if (traceEventStarted)
						{
							print "!controlEvent : the very last step by 2*stepLength was \Baccepted\B,\n";
							valueTrace();
						}
					}
					
					stateEventFinalization();
					
					if (traceEventStarted)
						print options= bold, italic "--- State Event Handling is done\n";
					
					reactivate list = waitUntilList;
					yield;
					
					if (savedStepLength >0.0) 
						stepLength = savedStepLength;
					savedStepLength = -1.0;
				}
				else
				{
					advance stepLength;
					
					///////////////////////////////////////////////////////////////////////
					for (dPtr = each DelayVariable in dList)
								dPtr->update();
					///////////////////////////////////////////////////////////////////////		
					
					for (rPtr = each IntegratorRecord in rList)
						rPtr->t += stepLength;
						
					if (traceStateStarted)
						valueTrace();
					
					// taking back the reduction of integration step width stepLength for the next step
					if (savedStepLength > 0.0)
					{
						stepLength = savedStepLength;
						savedStepLength = -1.0;
					}
					
					enlarge();
				}
			} //forever
		} //Actions
	} // class integrator
	
	class Euler_Forward_Integrator (double hh) subclass(Integrator (hh/10.0, hh, ABSOLUTE, 0.0)) {
				
		initial {
			stateEventHandling = TRUE;
			stepLength = maxStepLength;
		 }
		
		concrete method reduce() returning boolean {
			return FALSE;
		}

		concrete method enlarge() {
		}
		
		concrete method takeAStep (double h) {
		//..................................................................
			pointer (ContinuousVariable) 	vPtr;
			pointer (IntegratorRecord ) 	iPtr;
			
			// compute the rate for all active variable at: ( t )
			for (iPtr=each IntegratorRecord in rList)  { // compute the current rate at t
				iPtr->ode->derivatives(iPtr->t); 		
			}

			// compute the state at: ( t + h )
			for (vPtr = each ContinuousVariable in vList) {
								vPtr->state= vPtr->initialState + h * vPtr->rate; 
			}		
		} //takeAStep 
} // class EulerForward	


// Euler-Heun
//------------------------------------------------------------------------------------------------
	class euler_heun_integrator (double hh) subclass(Integrator (hh, hh, ABSOLUTE, 0.0)) {
		double	h_div2;			
		initial {
			stateEventHandling = TRUE;
			stepLength = maxStepLength;		
		}
		
		concrete procedure reduce() returning boolean {
			return FALSE;
		}

		concrete method enlarge() {
		}

		concrete procedure takeAStep (double h) {
		//..................................................................
			double h_div2 = stepLength/2.0;
			pointer (ContinuousVariable ) 	vPtr;
			pointer (IntegratorRecord ) 	iR;
			
			// compute the rate for all active variable at: ( t )
			for (iR=each IntegratorRecord in rList)  { // compute the current rate at t
				iR->ode->derivatives(iR->t); 		
			}

			// save the rate in k1 for all active variables 
			// and compute the state (prediction) at: ( t + h )
			for (vPtr=each ContinuousVariable in vList) {
				vPtr->k1= vPtr->rate; 	
				vPtr->state= vPtr->initialState + h *vPtr->k1; 
			}
					
			// compute the rate for all active variables at: ( t + h )
			for (iR=each IntegratorRecord in rList)  { // compute the current rate at t
				iR->ode->derivatives(iR->t+h); 		
			}

			// and compute the state (correction) at: ( t + h )
			for (vPtr=each ContinuousVariable in vList) {
				vPtr->state= vPtr->initialState + 
					h_div2 * ( vPtr->rate +  vPtr->k1); 		
			}		
		} //take_a_step
	} // class Euler-Heun	


	class RKM_Integrator (double min, max, ErrorType eT, double eL)
		subclass(Integrator (min, max, eT, eL))
	{
		initial
		{
			stateEventHandling = TRUE;
			stepLength = maxStepLength;
			}
		
		concrete method reduce() returning boolean
		{
			//..................................................................
			errorValue = errorNorm();
			
			if (errorValue <= errorLimit)
				return FALSE;
			
			if (stepLength < 2.0 * minStepLength)
			{
				errorMSG("Integrator", myName, "Reduce()", "has reached minStepLength", "");
				return FALSE;
			}
			
			return TRUE;
		} // reduce;
		
		concrete method enlarge()
		{
			if ((errorValue < 0.03125 * errorLimit) && (4.0*stepLength <= maxStepLength))
			{
				stepLength *= 2.0;
				if (traceStateStarted)
					print "**** STEP LENGTH EXTENSION ****\n";
			}
		}
		
		concrete method takeAStep(double stepLength)
		{
			//..................................................................
			double h_div2 = stepLength/2.0;
			double h_div3 = stepLength/3.0;
			double h_div6 = stepLength/6.0;
			double h_div8 = stepLength/8.0;

			pointer (ContinuousVariable) vPtr;
			pointer (IntegratorRecord) bPtr;
			
			// compute the rate for all active ContinuousVariable at: ( t )
			for (bPtr = each IntegratorRecord in rList)
				bPtr->ode->derivatives(bPtr->t);
			
			// save the rate in k1 for all active variables
			// and compute the state (prediction) at: ( t+h_div3 )
			for (vPtr = each ContinuousVariable in vList)
			{
				vPtr->k1 = vPtr->rate;
				vPtr->state = vPtr->initialState + h_div3*vPtr->k1;
			}
			
			// compute the rate for all active ContinuousVariable at: ( t + h_div3 )
			for (bPtr = each IntegratorRecord in rList)   // compute the current rate at t
				bPtr->ode->derivatives(bPtr->t + h_div3);
			
			// save the rate in k1 for all active variables
			// and compute the state (correction) at:  (t+h_div3)
			for (vPtr = each ContinuousVariable in vList)
			{
				vPtr->k2 = vPtr->rate;
				vPtr->state = vPtr->initialState + h_div6*(vPtr->k1 + vPtr->k2);
			}
			
			// compute the rate for all active ContinuousVariable at: ( t + h_div3 )
			for (bPtr = each IntegratorRecord in rList)    // compute the current rate at t
				bPtr->ode->derivatives(bPtr->t + h_div3);
			
			// save the rate in k2 for all active variables
			// and compute the state (prediction) at:  ( t+h_div2 )
			for (vPtr = each ContinuousVariable in vList)
			{
				vPtr->k2 = vPtr->rate;
				vPtr->state = vPtr->initialState + h_div8*(vPtr->k1 + 3.0*vPtr->k2);
			}
			
			//  compute the rate for all active ContinuousVariable at: ( t + h_div2 )
			for (bPtr = each IntegratorRecord in rList)   // compute the current rate at t
				bPtr->ode->derivatives(bPtr->t + h_div2);
			
			// save the rate in k3 for all active variables
			// and compute the state (prediction) at:  ( t + stepLength )
			for (vPtr = each ContinuousVariable in vList)
			{
				vPtr->k3 = vPtr->rate;
				vPtr->state = vPtr->initialState + h_div2*(vPtr->k1 - 3.0*vPtr->k2 + 4.0*vPtr->k3);
				vPtr->errorValue= vPtr->state;
			}
			
			//  compute the rate for all active ContinuousVariable at: ( t + stepLength )
			for (bPtr = each IntegratorRecord in rList)   // compute the current rate at t
				bPtr->ode->derivatives(bPtr->t + stepLength);
			
			// save the rate in k2 for all active variables
			// and compute the state (correction) at:  ( t + stepLength )
			// and use the state für error_computation
			for (vPtr = each ContinuousVariable in vList)
			{
				vPtr->k2 = vPtr->rate;
				vPtr->state = vPtr->initialState + h_div6*(vPtr->k1 + 4.0*vPtr->k3 + vPtr->k2);
				
				if (errorType == ABSOLUTE)
					vPtr->errorValue = abs(0.2*(vPtr->errorValue - vPtr->state));
				else if (vPtr->errorValue > 0.0)
					vPtr->errorValue = abs(0.2*(1.0 - vPtr->state/vPtr->errorValue));
			}
		} // take_a_step
	} // class Runge-Kutta-Merson
	
	procedure attachVar (pointer(ContinuousVariable) v)
		{
			v->myIntegrator = theIntegrator;
			v->isActive = TRUE;
			place v into theIntegrator->vList;
		}

	procedure attachODE(pointer(Behavior) ode)
		{
			place new IntegratorRecord(ode, 0) into theIntegrator->rList;
		}

	procedure attachPuck ( pointer(puck) p)
		{
			place p into theIntegrator->pList;
		}
		

} //end of module Continuous