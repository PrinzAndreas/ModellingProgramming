//*****************************************************************************
//	Big Autorerank Test
//*****************************************************************************

module BigAutoRerank
	{
#safeifdef WIN64
#endif

#safeifdef STUDENT_SLX
	constant int	NSAMP = 440;
#else
	constant int	NSAMP = 1000000;
#endif
	
	rn_stream	Samples;
	
	class widget
		{
		control double	x;
		control int		i;
		};
	
	set(widget) ranked(autorerank descending i, autorerank ascending x)		wset;
	
	procedure main()
		{
		double			tstart, fill_time, rerank_time;
		int			i;
		pointer(widget)	w, ww[NSAMP];		

		tstart = real_time();
		for (i = 1; i <= NSAMP; i++)
			{
			w = new widget;
			ww[i] = w;
			
			w -> i = rv_discrete_uniform(Samples, 1, NSAMP);
			w -> x = rv_uniform(Samples, 0, 1.0);
			
			place w into wset;
			}

		fill_time = real_time() - tstart;
		test_wset();
		
		//	Torture test: change all rerankable attributes

		tstart = real_time();
		for (i = 1; i <= NSAMP; i++)
			{
			w = ww[i];

			w -> i = rv_discrete_uniform(Samples, 1, NSAMP);
			w -> x = rv_uniform(Samples, 0, 1.0);
			}
		
		rerank_time = real_time() - tstart;
		
		test_wset();
		print options=bold (fill_time, rerank_time)	"\nFill Time: _.___ sec  Reranking Time: _.___\n\n";
		exit(0);
		}

	procedure test_wset()
		{
		pointer(widget)	w;
		
		int			j, previ, errors;
		double	prevx;
		
		previ = INT_MAX;
		prevx = -INFINITY;
		
		w = first widget in wset;
		print(w, w -> i, w -> x)				"\nFirst Member: _  i = _  x = _._____\n";
		
		w = last widget in wset;
		print(w, w -> i, w -> x)				"\nLast Member: _  i = _  x = _._____\n\n";
		
		j = 1;
		for (w = each widget in wset)
			{
			if (w -> i > previ)
				{
				++errors;
				print(j, w -> i, previ)		"Member _ is out-of-order:  i = _, previous i = _\n";
				}
			else
				if (w -> i == previ)
					if (w -> x < prevx)
						{
						++errors;
						print(j, w -> i, previ, w -> x, prevx)		"Member _ is out-of-order:  i = _, previous i = _  x = _._____, previous x = _._____\n";
						}
					
			previ = w -> i;
			prevx = w -> x;
			++j;
			}
		
		if (errors > 0)
			print options=bold,red	"\n_ Errors!\n";
		else
			print options=bold		"No errors\n";
			
		return;
		}
	}