//*****************************************************************************
//      Reneging - Time-Based Compound Wait Until Approach
//*****************************************************************************

import <h8>

module bank_with_reneging
	{
	queue			tellerq;
	storage 		tellers capacity=5;

	constant float	stop_time = 4800.0;		// 80 hours

	integer 		renege_count;
	rn_stream		stream1		seed=100000,
					stream2 		seed=200000;

//*****************************************************************************
//	Bank Model
//*****************************************************************************

	procedure main
		{
		arrivals: customer
			  iat = rv_expo(stream1, 0.2)	// 5 / minute
			  until_time = stop_time;

		advance stop_time;

		print(renege_count)
"\n\n"
"A total of * customers reneged\n";

		report(system);
		exit(0);
		}

	object customer
		{
		double		deadline;

		actions
			{
			enqueue tellerq;	// get in line

			deadline = time + 2.0;		// wait at most two minutes

			wait until(time == deadline or tellers.remaining > 0);

			if (time == deadline)
					{
					renege_count += 1;
					depart	tellerq;
					terminate;
					}

			enter	tellers;		// grab a teller
			depart	tellerq;		// exit queue

			advance rv_expo(stream2, 0.75);

			leave	tellers;		// free teller
			terminate;			// exit the bank
			}
		}
	}
