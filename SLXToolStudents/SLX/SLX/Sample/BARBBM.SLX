import <h8>

module barb_final	// ~GPSS/H Barbershop
    {
//*****************************************************************************
//  Global Data
//*****************************************************************************

    queue	   	 joeq	title = "Joe Q";

    facility	   	 joe 	title = "Joe";

    rn_stream	Arrivals		seed=100000,
				Service		seed=200000;

    constant float  stop_time = 4800 * 25;
    float  		nsamp = stop_time/18.0 * 1.10;
    double		samples[nsamp];	// global
    int 	    		scount;								// global index

//*****************************************************************************
//  Barbershop Model
//*****************************************************************************

    procedure main
	{
	arrivals: customer
		  iat = rv_uniform(Arrivals, 12.0, 24.0)
		  until_time = stop_time;

	wait until (time >= stop_time
	       and joeq.usage.current_value == 0
	       and joe.usage.current_value == 0);

	report system;

	report_batch_means_ci("\fQueue Time Batch Means", 0.9, samples, scount, 5, 200, 5);

	report_autocorrelation("\fEstimated autocorrelation", samples, scount, 20);

	report_batch_autocorrelation("\n\nBatch Autocorrelation (size 10)",
		samples, scount, 10, 20);

	report_batch_autocorrelation("\fBatch Autocorrelation (size 20)",
		samples, scount, 20, 20);

	report_batch_autocorrelation("\n\nBatch Autocorrelation (size 50)",
		samples, scount, 50, 20);

	exit(0);
	}

//*****************************************************************************
//  Customer Class
//*****************************************************************************

    class customer
	{
	actions
	    {
	    enqueue	    joeq;
	    seize	    joe;
		
	    samples[++scount] = time - ACTIVE -> mark_time;	// sample q time

	    depart	    joeq;
	    advance	    rv_uniform(Service, 12.0, 18.0);
	    release	    joe;
	    terminate;
	    }
	}

    }	// End of barb_final modul
